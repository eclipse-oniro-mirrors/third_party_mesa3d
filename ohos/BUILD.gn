import("//build/ohos.gni")
import("//build/config/clang/clang.gni")
mesa3d_libs_dir = "$root_build_dir/packages/phone/mesa3d"

mesa3d_gallium_symlinks = []
if (defined(product_build_config.mesa3d_gallium_symlinks)) {
    mesa3d_gallium_symlinks = product_build_config.mesa3d_gallium_symlinks
}

mesa3d_all_lib_items = [
    ["libEGL.so.1.0.0",         ["libEGL.so",       "libEGL.so.1"]],
    ["libgbm.so.1.0.0",         ["libgbm.so",       "libgbm.so.1"]],
    ["libglapi.so.0.0.0",       ["libglapi.so",     "libglapi.so.0"]],
    ["libGLESv1_CM.so.1.1.0",   ["libGLESv1_CM.so", "libGLESv1_CM.so.1"]],
    ["libGLESv2.so.2.0.0",      ["libGLESv2.so",    "libGLESv2.so.2", "libGLESv3.so"]],
    ["libgallium_dri.so",       mesa3d_gallium_symlinks]
]

action("mesa3d_build") {
    script = "build_mesa3d.py"
    deps = [
        "//foundation/graphic/standard/frameworks/surface:surface",
        "//third_party/expat:expat",
        "//third_party/wayland_standard:libwayland_client",
        "//third_party/wayland_standard:libwayland_server",
        "//third_party/wayland_standard:libwayland_client.0",
        "//third_party/wayland_standard:libwayland_server.0",
    ]
    outputs = []
    foreach(item, mesa3d_all_lib_items) {
        name = item[0]
        outputs += [ "$mesa3d_libs_dir/$name" ]
    }
    args = [ rebase_path(root_build_dir) ]
}

mesa3d_all_lib_deps = []

foreach(item, mesa3d_all_lib_items) {
    name = item[0]
    ohos_prebuilt_shared_library(name) {
        source = "$mesa3d_libs_dir/$name"
        deps = [ ":mesa3d_build" ]
        symlink_target_name = item[1]
        install_enable = true
        install_images = [ system_base_dir ]
        subsystem_name = "rockchip_products"
        part_name = "rockchip_products"
    }
    mesa3d_all_lib_deps += [ ":$name" ]
}

group("mesa3d_all_libs"){
    deps = mesa3d_all_lib_deps
}
