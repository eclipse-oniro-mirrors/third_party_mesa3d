# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("dependency_inputs.gni")

SUBSYSTEM_NAME="thirdparty"
PART_NAME="mesa3d"

action("mesa3d_action_build") {
  print("mesa3d_action_build")
  script = "//third_party/mesa3d/ohos/build_ohos64.py"
  inputs = deps_inputs
  outputs = [
    "${root_build_dir}/thirdparty/mesa3d/lib/libEGL_mesa.so",
    "${root_build_dir}/thirdparty/mesa3d/lib/libgallium-25.0.1.so",
  ]
  ohos_root_path = rebase_path("//")
  product_name = rebase_path("${root_build_dir}", "//out")
  mesa3d_source_path = rebase_path("//third_party/mesa3d", root_build_dir)

  if (is_asan) {
    if (use_hwasan) {
      asan_option = "hwasan"
    } else {
      asan_option = "swasan"
    }
  } else {
    asan_option = "noasan"
  }

  if (use_clang_coverage) {
    coverage_option = "use_clang_coverage"
  } else {
    coverage_option = "no_coverage"
  }
  if (mesa3d_feature_upgrade_skia) {
    skia_version = "new_skia"
  } else {
    skia_version = "skia"
  }
  args = [
    "$ohos_root_path",
    "$product_name",
    "$mesa3d_source_path",
    "$asan_option",
    "$coverage_option",
    "$skia_version"
  ]
  external_deps = [
    "hilog:libhilog",
    "graphic_surface:surface",
    "zlib:libz",
  ]
  if (mesa3d_feature_upgrade_skia) {
    external_deps += [ "skia:expatm133", ]
  } else {
    external_deps += [ "skia:expat", ]
  }
}

ohos_copy("mesa3d_unstripped_copy") {
  sources = [
    "${root_build_dir}/thirdparty/mesa3d/lib/libEGL_mesa.so",
    "${root_build_dir}/thirdparty/mesa3d/lib/libgallium-25.0.1.so",
  ]
  outputs = [ root_out_dir + "/lib.unstripped/thirdparty/mesa3d/{{source_file_part}}" ]
  deps = [ ":mesa3d_action_build" ]
}

ohos_prebuilt_shared_library("mesa3d_libEGL") {
  source = "${root_build_dir}/thirdparty/mesa3d/lib/libEGL_mesa.so"
  subsystem_name = "$SUBSYSTEM_NAME"
  part_name = "$PART_NAME"
  module_install_dir = "lib64"
  install_enable = true
  enable_strip = true
  mini_debug = true
  deps = [
    ":mesa3d_action_build",
    ":mesa3d_unstripped_copy"
  ]
}

ohos_prebuilt_shared_library("mesa3d_libgallium") {
  source = "${root_build_dir}/thirdparty/mesa3d/lib/libgallium-25.0.1.so"
  subsystem_name = "$SUBSYSTEM_NAME"
  part_name = "$PART_NAME"
  module_install_dir = "lib64"
  install_enable = true
  enable_strip = true
  mini_debug = true
  deps = [
    ":mesa3d_action_build",
    ":mesa3d_unstripped_copy"
  ]
}

group("zink_opengl") {
  deps = [
    ":mesa3d_libEGL",
    ":mesa3d_libgallium",
  ]
}
